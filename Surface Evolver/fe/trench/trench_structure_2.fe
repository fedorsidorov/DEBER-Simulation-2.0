// File: my_structure.fe
// Date: 20.02.2018
// Code: alex isaev
// Surface Evolver data for full 3D reflow simulation double-step line
// Model parameterized in (uks)-units (um, kg, s)

/*------------------------------------HEADER START------------------------------------*/
/*defining a general value mobility*/
define vertex attribute vmob real 		// required for mobility_tensor (must be a vertex attribute in this tensor)
define facet attribute fmob real		// used as transfer parameter to inherit vertex mobility from facet mobility

/*mobility tensor m*/
MOBILITY_TENSOR
vmob 	0 	0
0 	vmob 	0
0	0	vmob

/*defining simulation control parameters*/
PARAMETER Temp = 150 	// Reflow temperature

PARAMETER countr = 0	// running counter used to inherit vertex mobility via average over facets	
PARAMETER mobsum = 0	// running sum used to inherit vertex mobility via average over facets	

PARAMETER IXu = 4		// used to print and visualize facets having at least one vertex with this mobility
PARAMETER IXo = 5		// used to print and visualize facets having at least one vertex with this mobility

/*defining geometry and material parameters*/
#include "trench_geometry_2.fe"	// include the file with the required geometry data

/*defining macros*/
#define GR 9.81e6                               // gravity constant
#define SG 1/(SV+gamma*Temp)                    // specific gravity of resist

#define step_volume (res_h_max*step_hw_bas-(delta*(res_h_max-res_h_min))/1)*step_l

/*initialization*/
gravity_constant GR  

/*------------------------------------HEADER END------------------------------------*/


/*##################################################################################*/


/*---------------------------------CONSTRAINTS START--------------------------------*/
constraint 1   /* fixing the resist on the substrate surface */
formula: x3 = 0

constraint 2
formula: x3 = pin_h

constraint 3 nonpositive /* pinning resist on upper end of un-exposed PMMA pillar*/
formula: x3 = pin_h

constraint 4   /* mirror plane, resist on front-side wall */
formula: x1 = 0.5*step_l

constraint 5   /* mirror plane, resist on back-side wall */
formula: x1 = -0.5*step_l

constraint 6
formula: x2 = 0.5*step_hw_bas

constraint 7
formula: x2 = -0.5*step_hw_bas

/*---------------------------------CONSTRAINTS END----------------------------------*/


/*##################################################################################*/


/*---------------------------------GEOMETRY START-----------------------------------*/
// (0 0 0) is at the step center and at the bottom


vertices
// for limitation of simulation area
31     0.5*step_l    -0.5*base_w         0.0          vmob    0          fixed    /* for table top */
32    -0.5*step_l    -0.5*base_w         0.0          vmob    0          fixed
33     0.5*step_l     0.5*base_w         0.0          vmob    0          fixed
34    -0.5*step_l     0.5*base_w         0.0          vmob    0          fixed

// vertices for step
// bottom
1      0.5*step_l    -0.5*step_hw_bas    0.0          vmob    vmob_v    constraints 1 4
2     -0.5*step_l    -0.5*step_hw_bas    0.0          vmob    vmob_v    constraints 1 5
3      0.5*step_l     0.5*step_hw_bas    0.0          vmob    vmob_v    constraints 1 4
4     -0.5*step_l     0.5*step_hw_bas    0.0          vmob    vmob_v    constraints 1 5

// top
5      0.5*step_l  	 -0.5*step_hw_bas    res_h_max    vmob    vmob_v    constraints 3 4
6     -0.5*step_l  	 -0.5*step_hw_bas    res_h_max    vmob    vmob_v    constraints 3 5

//7      0.5*step_l  	 -0.5*delta          res_h_mid    vmob    vmob_v    constraints 3 4
//8     -0.5*step_l  	 -0.5*delta          res_h_mid    vmob    vmob_v    constraints 3 5
//11     0.5*step_l  	  0.5*delta          res_h_mid    vmob    vmob_v    constraints 3 4
//12    -0.5*step_l  	  0.5*delta          res_h_mid    vmob    vmob_v    constraints 3 5

7      0.5*step_l  	 -0.5*delta-buf      res_h_mid    vmob    vmob_v    constraints 3 4
8     -0.5*step_l  	 -0.5*delta-buf      res_h_mid    vmob    vmob_v    constraints 3 5
11     0.5*step_l  	  0.5*delta+buf      res_h_mid    vmob    vmob_v    constraints 3 4
12    -0.5*step_l  	  0.5*delta+buf      res_h_mid    vmob    vmob_v    constraints 3 5

13     0.5*step_l  	  0.5*step_hw_bas    res_h_max    vmob    vmob_v    constraints 3 4
14    -0.5*step_l  	  0.5*step_hw_bas    res_h_max    vmob    vmob_v    constraints 3 5

57     0.5*step_l  	 -0.5*delta-buf      res_h_max    vmob    vmob_v    constraints 3 4
58    -0.5*step_l  	 -0.5*delta-buf      res_h_max    vmob    vmob_v    constraints 3 5
51     0.5*step_l  	  0.5*delta+buf      res_h_max    vmob    vmob_v    constraints 3 4
52    -0.5*step_l  	  0.5*delta+buf      res_h_max    vmob    vmob_v    constraints 3 5



// middle
103    0.5*step_l  	  0.5*delta               0            vmob    vmob_v    constraint 4
204   -0.5*step_l  	  0.5*delta               0            vmob    vmob_v    constraint 5
9      0.5*step_l  	  0.5*delta               res_h_min    vmob    vmob_v    constraint 4
10    -0.5*step_l  	  0.5*delta               res_h_min    vmob    vmob_v    constraint 5
43     0.5*step_l  	 -0.5*delta               0            vmob    vmob_v    constraint 4
44    -0.5*step_l  	 -0.5*delta               0            vmob    vmob_v    constraint 5
49     0.5*step_l  	 -0.5*delta               res_h_min    vmob    vmob_v    constraint 4
40    -0.5*step_l  	 -0.5*delta               res_h_min    vmob    vmob_v    constraint 5


edges  
// given by endpoints and attributes
// for limitation of simulation area
41    31    32    no_refine    fixed  /* for table top */
42    32    34    no_refine    fixed
43    34    33    no_refine    fixed
44    33    31    no_refine    fixed

1      1     2    constraint 1 
2      5     6    constraints 3 2
3      7     8    constraint 3
4      9    10
5     11    12    constraint 3
6     13    14    constraints 3 2
7      3     4    constraint 1
8      1     5    constraints 4 7
9      2     6    constraints 5 7
10   103     9    constraint 4
11   204    10    constraint 5
12     3    13    constraint 4 6
13     4    14    constraint 5 6
14     5    57    constraints 3 4
15     6    58    constraints 3 5
16     7    49
17     8    40
18     9    11
19    10    12
20    51    13    constraints 3 4
21    52    14    constraints 3 5
22     1    43    constraints 1 4
23     2    44    constraints 1 5
24     3   103    constraints 1 4
25     4   204    constraints 1 5

26   103   204    constraint 1

27    43    49    constraint 4
28    44    40    constraint 5
31    43   103    constraints 1 4
32    44   204    constraints 1 5
33    49    40
34    43    44    constraint 1
35    49     9
36    40    10

53    57    58    constraint 3
52    51    52    constraint 3
50    11    51    constraints 3 4
51    12    52    constraints 3 5
54    57     7    constraints 3 4
55    58     8    constraints 3 5

faces  
// given by oriented edge loop
// for limitation of simulation area
// wall to the left and base below (required to avoid vertices having zero facets)
100     41   42   43   44    color clear    tension 0    no_refine    density 0    fmob 0    fixed /* table top */

// side
1      8   2  -9  -1    color green     tension TENS_r    fmob fmob_v    constraint 7
2     14  53 -15  -2    color green     tension TENS_r    fmob fmob_v
52    54   3 -55 -53    color brown     tension TENS_r    fmob fmob_v
3     16  33 -17  -3    color yellow    tension TENS_r    fmob fmob_v
4     33  36  -4 -35    color yellow    tension TENS_r    fmob fmob_v
5     18   5 -19  -4    color yellow    tension TENS_r    fmob fmob_v
56     50  52 -51  -5    color brown     tension TENS_r    fmob fmob_v
6     20   6 -21 -52    color green     tension TENS_r    fmob fmob_v
7      7  13  -6 -12    color green     tension TENS_r    fmob fmob_v    constraint 6

// mirror planes
8     22  27 -16 -54 -14 -8    tension TENS_mirr    fmob fmob_v    constraint 4
9      9  15  55  17 -28 -23   tension TENS_mirr    fmob fmob_v    constraint 5
10    12 -20 -50 -18 -10 -24   tension TENS_mirr    fmob fmob_v    constraint 4
11    25  11  19  51  21 -13   tension TENS_mirr    fmob fmob_v    constraint 5
12    32  11 -36 -28           tension TENS_mirr    fmob fmob_v    constraint 5
13    31  10 -35 -27           tension TENS_mirr    fmob fmob_v    constraint 4


// bottom
14     1  23 -34 -22   tension TENS_unexp    fmob fmob_v
15    34  32 -26 -31   tension TENS_unexp    fmob fmob_v
16    24  26 -25  -7   tension TENS_unexp    fmob fmob_v


bodies  
// body defined by its oriented faces
1    1 2 52 3 4 5 56 6 7    volume step_volume    density SG


/*---------------------------------GEOMETRY END-----------------------------------*/


/*##################################################################################*/


/*----------------------------------simulation--------------------------------------*/
/*defining commands for SE command line execution*/
read 
/*calculate vertex mobility as average of mobility all surrounding facets*/
defv:={foreach vertex vv do {countr:=0; mobsum:=0; foreach vv.facet do {countr:=countr+1; mobsum:=mobsum+fmob}; print id; print mobsum/countr; set vv vmob mobsum/countr}}

/*print list of vertices (IDs) with a mobility in the given range*/
liste:={foreach vertex vv where vv.vmob>=IXu and vv.vmob<=IXo do {print id}}

/*color alls facets having at least one vertices with a mobility in the given range*/
colr:={foreach vertex vv where vv.vmob>=IXu and vv.vmob<=IXo do {foreach vv.facet do set vv.facet color red}}

/*graphical output of slicing along z-y-plane at x=0*/
sliceX := {slice_coeff[1]:= 1; 
           slice_coeff[2]:= 0; 
           slice_coeff[3]:= 0;
           slice_coeff[4]:= 0;
           slice_view}

/*meshing*/
meshit_1 := {r; defv; area_normalization;}
meshit := {r; defv; r; defv; r; defv; area_normalization;}

/*iteration*/
loopit := {g50; w 0.001; w 0.001; V}
//loopit := {g50; w 0.001; w 0.001}

dropdata := {foreach vertex do printf "%d\t%f\t%f\t%f\n", id, x, y, z >> "/Users/fedor/Documents/DEBER-Simulation-2.0/Surface Evolver/fe/trench/dose1.txt"}

// foreach vertex do printf "%d\t%f\t%f\t%f\n", id, x, y, z >> "/Users/fedor/Documents/DEBER-Simulation-2.0/Surface Evolver/fe/trench/dose2.txt"


